///$tab Main
SET ThousandSep=' ';
SET DecimalSep=',';
SET MoneyThousandSep=' ';
SET MoneyDecimalSep=',';
SET MoneyFormat='# ##0,00 €;-# ##0,00 €';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD/MM/YYYY';
SET TimestampFormat='DD/MM/YYYY hh:mm:ss[.fff]';
SET FirstWeekDay=0;
SET BrokenWeeks=0;
SET ReferenceDay=4;
SET FirstMonthOfYear=1;
SET CollationLocale='fr-FR';
SET CreateSearchIndexOnReload=1;
SET MonthNames='janv.;févr.;mars;avr.;mai;juin;juil.;août;sept.;oct.;nov.;déc.';
SET LongMonthNames='janvier;février;mars;avril;mai;juin;juillet;août;septembre;octobre;novembre;décembre';
SET DayNames='lun.;mar.;mer.;jeu.;ven.;sam.;dim.';
SET LongDayNames='lundi;mardi;mercredi;jeudi;vendredi;samedi;dimanche';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:μ;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';


LET vDate_today = MakeDate(2014, 12, 31) * 1;

// https://www.kaggle.com/datasets/ahsan81/hotel-reservations-classification-dataset


///$tab Prep
hotel_reservations_prep:
LOAD
  Rand() as alea,
  Booking_ID as booking_id,
  no_of_adults,
  no_of_children,
  no_of_weekend_nights,
  no_of_week_nights,
  type_of_meal_plan,
  required_car_parking_space,
  room_type_reserved,
  lead_time,
//   arrival_year,
  arrival_month,
  arrival_date as arrival_day,
  Text(WeekDay(MakeDate(arrival_year, arrival_month, arrival_date))) as arrival_weekday,
  market_segment_type,
  repeated_guest,
  no_of_previous_cancellations,
  no_of_previous_bookings_not_canceled,
  avg_price_per_room,
  no_of_special_requests,
  booking_status
FROM [lib://demo_ml:DataFiles/raw/Hotel Reservations.csv]
(txt, codepage is 28591, embedded labels, delimiter is ',', msq)
;


LET vSplit = .95;


hotel_reservations_train:
NoConcatenate
Load
  booking_id AS train_id,
  *
Resident hotel_reservations_prep
Where alea <= $(vSplit)
;


hotel_reservations_test:
NoConcatenate
Load
  *
Resident hotel_reservations_prep
// Where alea > $(vSplit)
Where not Exists(train_id, booking_id)
;


Drop Table hotel_reservations_prep;
Drop Field alea;
Drop Field train_id From hotel_reservations_train;


hotel_reservations_test_result:
Load
  booking_id,
  booking_status
Resident hotel_reservations_test
;


Drop Field booking_status From hotel_reservations_test;


Store hotel_reservations_train into [lib://demo_ml:DataFiles/prep/hotel_reservations_train.qvd] (qvd);
Store hotel_reservations_test into [lib://demo_ml:DataFiles/prep/hotel_reservations_test.qvd] (qvd);
Store hotel_reservations_test_result into [lib://demo_ml:DataFiles/prep/hotel_reservations_test_result.qvd] (qvd);


Drop Tables hotel_reservations_train, hotel_reservations_test, hotel_reservations_test_result;

